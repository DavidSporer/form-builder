prototype(Neos.Form.Builder:NodeBasedForm) < prototype(Neos.Form.Builder:Form) {
    @context.formNode = ${q(node).closest('[instanceof Neos.Form.Builder:NodeBasedForm]').get(0)}
    @context.formFusionPath = ${this.path}
    identifier = ${formNode.properties.identifier ? formNode.properties.identifier : 'form-' + formNode.identifier}
    presetName = ${formNode.properties.preset ? formNode.properties.preset : 'default'}
    formElementType = ${formNode.context.inBackend ? 'Neos.Form:FormEditMode' : 'Neos.Form:Form'}

    renderCallbacks.formElementWrapping = Neos.Form.Builder:FormElementWrapping {
        @if.isInBackend = ${formNode.context.inBackend}
    }

    firstPage {
        label = ${formNode.properties.label}
        renderingOptions._node = ${formNode}
        renderingOptions._fusionPath = ${formFusionPath}
        elements = Neos.Form.Builder:ElementCollection {
            collection = ${q(formNode).children('elements').children()}
        }
    }
    furtherPages = Neos.Form.Builder:PageCollection {
        collection = ${q(formNode).children('furtherPages').children()}
    }
    finishers = Neos.Form.Builder:FinisherCollection {
        collection = ${q(formNode).children('finishers').children()}
    }


    #    @cache {
    #        mode = 'dynamic'
    #        entryIdentifier {
    #            node = ${node}
    #        }
    #        entryDiscriminator = ${request.httpRequest.methodSafe ? 'static' : false}
    #        context {
    #            1 = 'node'
    #            2 = 'documentNode'
    #        }
    #        entryTags {
    #            1 = ${'Node_' + node.identifier}
    #            2 = ${'DescendantOf_' + node.identifier}
    #        }
    #    }
    @cache {
        mode = 'uncached'
        context {
            1 = 'node'
            2 = 'documentNode'
        }
    }
    @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}

prototype(Neos.Form.Builder:ElementCollection) < prototype(Neos.Fusion:Collection) {

    itemName = 'elementNode'
    itemRenderer = Neos.Fusion:Renderer {
        type = ${elementNode.nodeType.name}
        element {
            @context.element = ${this}
            identifier = ${elementNode.properties.identifier ? elementNode.properties.identifier : elementNode.identifier}
            label = ${elementNode.properties.label}
            required = ${elementNode.properties.required}
            defaultValue = ${elementNode.properties.defaultValue}
            properties.@process.addNodeProperties = ${Array.concat(value, elementNode.properties)}
            validators = Neos.Form.Builder:ValidatorCollection {
                collection = ${q(elementNode).children('validators').children()}
            }
            renderingOptions._node = ${elementNode}
            renderingOptions._fusionPath = ${element.path}

            properties.options {
                collection = ${q(elementNode).children('options').children()}
                valuePropertyPath = 'properties.value'
                labelPropertyPath = 'properties.label'
                @if.isSelectFormElement = ${q(elementNode).is('[instanceof Neos.Form.Builder:SelectionMixin]')}
            }
        }
    }
}


prototype(Neos.Form.Builder:PageCollection) < prototype(Neos.Fusion:Collection) {
    itemName = 'pageNode'
    itemRenderer = Neos.Form.Builder:FormPage {
        @context.page = ${this}
        identifier = ${pageNode.properties.identifier ? pageNode.properties.identifier : pageNode.identifier}
        label = ${pageNode.properties.label}
        formElementType = ${pageNode.nodeType.options.form.formElementType}
        renderingOptions._node = ${pageNode}
        renderingOptions._fusionPath = ${page.path}

        elements = Neos.Form.Builder:ElementCollection {
            collection = ${q(pageNode).children('elements').children()}
        }
    }
}

prototype(Neos.Form.Builder:FinisherCollection) < prototype(Neos.Fusion:Collection) {
    itemName = 'finisherNode'
    itemRenderer = Neos.Fusion:Renderer {
        type = ${finisherNode.nodeType.name}
        element {
            options.@process.addNodeProperties = ${Array.concat(value, finisherNode.properties)}
        }
    }
}

prototype(Neos.Form.Builder:ValidatorCollection) < prototype(Neos.Fusion:Collection) {
    itemName = 'validatorNode'
    itemRenderer = Neos.Fusion:Renderer {
        type = ${validatorNode.nodeType.name}
        element {
            options.@process.addNodeProperties = ${Array.concat(value, validatorNode.properties)}
        }
    }
}

prototype(Neos.Form.Builder:FormElementWrapping) {
    @class = 'Neos\\Form\\Builder\\Fusion\\FormElementWrappingImplementation'
}